#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТолькоПросмотр = НЕ ПравоДоступа("Изменение", Метаданные.ПланыСчетов.Хозрасчетный);
	Элементы.ЗаписатьИЗакрыть.Видимость = НЕ ТолькоПросмотр;
	
	ПараметрыУчета = ОбщегоНазначенияБПВызовСервера.ОпределитьПараметрыУчета();
	
	УчитыватьРасходыПоЭлементамЗатрат                 = ПараметрыУчета.УчитыватьРасходыПоЭлементамЗатрат;
	УчитыватьРасходыПоЭлементамЗатратИсходноеЗначение = УчитыватьРасходыПоЭлементамЗатрат;
	
	УчитыватьРасходыПоСтатьямЗатрат                 = ПараметрыУчета.УчитыватьРасходыПоСтатьямЗатрат;
	УчитыватьРасходыПоСтатьямЗатратИсходноеЗначение = УчитыватьРасходыПоСтатьямЗатрат;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		Отказ = Истина;
		
		ТекстВопроса = НСтр("ru='Данные были изменены. Сохранить изменения?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоЭлементамЗатратФлагПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоСтатьямЗатратФлагПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьИзменения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	УстановитьСтраницуПредупрежденияПоЭлементамЗатрат(Форма);	
	УстановитьСтраницуПредупрежденияПоСтатьямЗатрат(Форма);	
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗаписьюЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПрименитьНастройкуСубконто();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаписатьИзменения();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИзменения()
	
	ПараметрыУчета.УчитыватьРасходыПоЭлементамЗатрат = УчитыватьРасходыПоЭлементамЗатрат;
	ПараметрыУчета.УчитыватьРасходыПоСтатьямЗатрат   = УчитыватьРасходыПоСтатьямЗатрат;
	
	ТекстВопроса = ОбщегоНазначенияБПВызовСервера.ПолучитьТекстВопросаПриУдаленииСубконто(ПараметрыУчета);
	Если ПустаяСтрока(ТекстВопроса) Тогда
		ПрименитьНастройкуСубконто();
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВопросПередЗаписьюЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьНастройкуСубконто(Отказ = Ложь)
	
	ПрименитьНастройкуСубконтоНаСервере(Отказ);
	
	Если НЕ Отказ Тогда
		Модифицированность = Ложь;
		Оповестить("ИзменениеНастройкиПланаСчетов");
		Оповестить("ИзменениеНастроекПараметровУчета");
		ОповеститьОбИзменении(Тип("ПланСчетовСсылка.Хозрасчетный"));
		ПоказатьОповещениеПользователя(НСтр("ru = 'Изменены параметры субконто'"), 
			"e1cib/app/Обработка.ЖурналРегистрации", "Журнал регистрации");
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьНастройкуСубконтоНаСервере(Отказ = Ложь)
	
	ОбщегоНазначенияБПВызовСервера.ПрименитьПараметрыУчета(ПараметрыУчета, Истина, Отказ);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСтраницуПредупреждения(Значение, ИсходноеЗначение, ГруппаСтраниц, СтраницаВключено, СтраницаОтключено)
	
	ПользовательИзменилЗначение = Значение <> ИсходноеЗначение;
	Предупредить                = ИсходноеЗначение И ПользовательИзменилЗначение;
	
	Если Предупредить Тогда
		ГруппаСтраниц.ТекущаяСтраница = СтраницаВключено;
	Иначе
		ГруппаСтраниц.ТекущаяСтраница = СтраницаОтключено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСтраницуПредупрежденияПоЭлементамЗатрат(Форма)
	
	УстановитьСтраницуПредупреждения(
		Форма.УчитыватьРасходыПоЭлементамЗатрат,
		Форма.УчитыватьРасходыПоЭлементамЗатратИсходноеЗначение,
		Форма.Элементы.ПоЭлементамЗатратПредупреждение,
		Форма.Элементы.ПоЭлементамЗатратПредупреждениеВключено,
		Форма.Элементы.ПоЭлементамЗатратПредупреждениеОтключено);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСтраницуПредупрежденияПоСтатьямЗатрат(Форма)
	
	УстановитьСтраницуПредупреждения(
		Форма.УчитыватьРасходыПоСтатьямЗатрат,
		Форма.УчитыватьРасходыПоСтатьямЗатратИсходноеЗначение, 
		Форма.Элементы.ПоСтатьямЗатратПредупреждение, 
		Форма.Элементы.ПоСтатьямЗатратПредупреждениеВключено, 
		Форма.Элементы.ПоСтатьямЗатратПредупреждениеОтключено);
	
КонецПроцедуры

#КонецОбласти
