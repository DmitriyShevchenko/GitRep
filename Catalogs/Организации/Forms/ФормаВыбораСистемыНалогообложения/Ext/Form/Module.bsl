#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ГруппаОбособленноеПодразделение.Видимость = ПолучитьФункциональнуюОпцию("ВестиУчетПоПодразделениям");
	
	РежимПовторногоВыбораВидаОрганизации = Параметры.РежимПовторногоВыбораВидаОрганизации;
	РежимПовторногоВыбораСистемыНалогообложения = Параметры.РежимПовторногоВыбораСистемыНалогообложения;
	
	Если Параметры.Свойство("ВидОрганизации") Тогда
		ВидОрганизации = Параметры.ВидОрганизации;
	КонецЕсли;
	
	ПараметрыУчетнойПолитики = Новый Структура;
	                                                
	Если Параметры.Свойство("СистемаНалогообложения") Тогда
		ПараметрыУчетнойПолитики.Вставить("СистемаНалогообложения", Параметры.СистемаНалогообложения);
	КонецЕсли;
	
	Если Параметры.Свойство("ОбъектНалогообложенияУСН") Тогда
		ПараметрыУчетнойПолитики.Вставить("ОбъектНалогообложенияУСН", Параметры.ОбъектНалогообложенияУСН);
	КонецЕсли;
	
	Если Параметры.Свойство("ПлательщикЕНВД") Тогда
		ПараметрыУчетнойПолитики.Вставить("ПлательщикЕНВД", Параметры.ПлательщикЕНВД);
	КонецЕсли;

	Если Параметры.Свойство("ПрименяетсяУСНПатент") Тогда
		ПараметрыУчетнойПолитики.Вставить("ПрименяетсяУСНПатент", Параметры.ПрименяетсяУСНПатент);
	КонецЕсли;
	
	// Ограничение по доходам
	ТекстОграниченияДоходы = СтрШаблон(НСтр("ru='%1 %2'"),
		Формат(КонтрольПраваПримененияСпецрежима.ГраницаДоходовОграничивающаяПравоПримененияУСН(ТекущаяДатаСеанса()),"ЧДЦ=2; ЧС=6"),
		НСтр("ru='млн руб'"));
	
	Элементы.НадписьДоходыИП.Заголовок = ТекстОграниченияДоходы;
	Элементы.НадписьДоходыУСНЮЛ.Заголовок = ТекстОграниченияДоходы;
	
	// Ограничение по стоимости основных средств
	ТекстОграниченияОС = СтрШаблон(НСтр("ru='%1 %2'"),
		Формат(КонтрольПраваПримененияСпецрежима.ГраницаСтоимостиОсновныхСредствОграничивающаяПравоПримененияУСН(),"ЧС=6"),
		НСтр("ru='млн руб'"));
	Элементы.НадписьОсновныеСредстваУСНЮЛ.Заголовок = ТекстОграниченияОС;
	
	// Ограничение по среднесписочной численности работников
	ТекстОграничениеРаботникиУСН = СтрШаблон(НСтр("ru='%1 %2'"),
		КонтрольПраваПримененияСпецрежима.ГраницаСреднесписочнойЧисленностиРаботниковОграничивающаяПравоПримененияУСН(),
		"человек");
	Элементы.НадписьРаботникиУСНИП.Заголовок = ТекстОграничениеРаботникиУСН;
	Элементы.НадписьРаботникиУСНЮЛ.Заголовок = ТекстОграничениеРаботникиУСН;
	
	ТекстОграничениеРаботникиПатент = СтрШаблон(НСтр("ru='%1 %2'"),
		КонтрольПраваПримененияСпецрежима.ГраницаСреднесписочнойЧисленностиРаботниковОграничивающаяПравоПримененияПатент(),
		"человек");
	Элементы.НадписьРаботникиПатентИП.Заголовок = ТекстОграничениеРаботникиПатент;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ВидОрганизации <> "ОбособленноеПодразделение" Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГоловнаяОрганизация");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Заголовок = ПолучитьЗаголовокФормы();
	
	Если РежимПовторногоВыбораВидаОрганизации Тогда
		ИнициализироватьФормуВРежимеПовторногоВыбораВидаОрганизации();
	ИначеЕсли РежимПовторногоВыбораСистемыНалогообложения Тогда
		ИнициализироватьФормуВРежимеПовторногоВыбораСистемыНалогообложения();
	Иначе
		Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаВидОрганизации;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОткрытиеФормыНовойОрганизации" 
	   И Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьИП(Команда)
	
	ВидОрганизации = "ИндивидуальныйПредприниматель";
	
	Если РежимПовторногоВыбораВидаОрганизации Тогда
		Закрыть(ВидОрганизации);
	КонецЕсли;
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаСистемаНалогообложенияИП;
	
	Заголовок = ПолучитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЮЛ(Команда)
	
	ВидОрганизации = "ЮридическоеЛицо";
	
	Если РежимПовторногоВыбораВидаОрганизации Тогда
		Закрыть(ВидОрганизации);
	КонецЕсли;
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаСистемаНалогообложенияЮЛ;
	
	Заголовок = ПолучитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОП(Команда)
	
	ВидОрганизации = "ОбособленноеПодразделение";
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаСистемаНалогообложенияОП;
	
	Заголовок = ПолучитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияОбщая(Команда)
	
	СистемаНалогообложения = "Общая";
	
	ОткрытьФормуОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияПатентная(Команда)
	
	СистемаНалогообложения = "Патентная";
	
	ОткрытьФормуОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияУСНДоходы(Команда)
	
	СистемаНалогообложения = "УпрощеннаяДоходы";
	
	ОткрытьФормуОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияУСНДоходыМинусРасходы(Команда)
	
	СистемаНалогообложения = "УпрощеннаяДоходыМинусРасходы";
	
	ОткрытьФормуОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаВидОрганизации;
	
	Заголовок = ПолучитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОрганизацию(Команда)
	
	ОткрытьФормуОрганизации();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ГиперссылкаСравнениеСистемНалогообложенияНажатие(Элемент)

	ЮридическоеФизическоеЛицо = ?(ВидОрганизации = "ИндивидуальныйПредприниматель",
		ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо"),
		ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо"));
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ВидОрганизации", ЮридическоеФизическоеЛицо);
	
	ОткрытьФорму("Обработка.СравнениеРежимовНалогообложения.Форма", СтруктураПараметров, ЭтаФорма, ВидОрганизации);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТекущаяСтраницаПоВидуОрганизации(ВидОрганизации, РежимПовторногоВыбораСистемыНалогообложения)
	
	Если ВидОрганизации = "ЮридическоеЛицо" Тогда
		ТекущаяСтраницаФормы = "СтраницаСистемаНалогообложенияЮЛ";
	ИначеЕсли ВидОрганизации = "ИндивидуальныйПредприниматель" Тогда
		ТекущаяСтраницаФормы = "СтраницаСистемаНалогообложенияИП";
	ИначеЕсли ВидОрганизации = "ОбособленноеПодразделение" Тогда
		Если РежимПовторногоВыбораСистемыНалогообложения Тогда
			ВызватьИсключение "Для обособленного подразделения система налогообложения определяется головной организацией.";
		Иначе
			ТекущаяСтраницаФормы = "СтраницаСистемаНалогообложенияОП";
		КонецЕсли;
	Иначе
		ТекущаяСтраницаФормы = "СтраницаВидОрганизации";
	КонецЕсли;
	Возврат ТекущаяСтраницаФормы;
	
КонецФункции

&НаКлиенте
Функция ПолучитьЗаголовокФормы()
	
	Если РежимПовторногоВыбораСистемыНалогообложения Тогда
		
		ЗаголовокФормы = НСтр("ru='Система налогообложения'");
		
	Иначе
	
		ЗаголовокФормы = НСтр("ru = 'Вид организации'");
		
		Если Элементы.СтраницыФормы.ТекущаяСтраница <> Элементы.СтраницаВидОрганизации Тогда
			Если ВидОрганизации = "ЮридическоеЛицо" Тогда
				ЗаголовокФормы = НСтр("ru = 'Юридическое лицо'");
			ИначеЕсли ВидОрганизации = "ИндивидуальныйПредприниматель" Тогда
				ЗаголовокФормы = НСтр("ru = 'Индивидуальный предприниматель'");
			ИначеЕсли ВидОрганизации = "ОбособленноеПодразделение" Тогда
				ЗаголовокФормы = НСтр("ru = 'Обособленное подразделение'");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ЗаголовокФормы;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьИННГоловнойОрганизации(Организация)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ИНН")	
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуОрганизации()
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураУчетнойПолитики = ПолучитьСтруктуруУчетнойПолитики(ВидОрганизации,	СистемаНалогообложения, ПлательщикЕНВД);
	
	Модифицированность = Ложь;
	
	Если РежимПовторногоВыбораСистемыНалогообложения Тогда
		
		Закрыть(СтруктураУчетнойПолитики);
		
	Иначе
	
		ЗначенияЗаполнения = Новый Структура;
		Если ВидОрганизации = "ИндивидуальныйПредприниматель" Тогда
			ЗначенияЗаполнения.Вставить("ЮридическоеФизическоеЛицо",
				ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо"));
		Иначе
			ЗначенияЗаполнения.Вставить("ЮридическоеФизическоеЛицо",
				ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо"));
		КонецЕсли;
		
		ЗначенияЗаполнения.Вставить("ОбособленноеПодразделение", (ВидОрганизации = "ОбособленноеПодразделение"));
		Если ВидОрганизации = "ОбособленноеПодразделение" Тогда
			ЗначенияЗаполнения.Вставить("ГоловнаяОрганизация", ГоловнаяОрганизация);
			ЗначенияЗаполнения.Вставить("ИНН", ПолучитьИННГоловнойОрганизации(ГоловнаяОрганизация));
		КонецЕсли;
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ЗначенияЗаполнения",       ЗначенияЗаполнения);
		СтруктураПараметров.Вставить("СтруктураУчетнойПолитики", СтруктураУчетнойПолитики);
		
		ОткрытьФорму("Справочник.Организации.Форма.ФормаОрганизации", СтруктураПараметров, ВладелецФормы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИнициализироватьФормуВРежимеПовторногоВыбораВидаОрганизации()
		
	Элементы.ГруппаОбособленноеПодразделение.Видимость = Ложь;
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаВидОрганизации;
	
	КнопкаПоУмолчанию = Новый Соответствие;
	КнопкаПоУмолчанию["ЮридическоеЛицо"]               = "ВыбратьЮЛ";
	КнопкаПоУмолчанию["ИндивидуальныйПредприниматель"] = "ВыбратьИП";
	
	ЭлементФормыПоУмолчанию = Элементы[КнопкаПоУмолчанию[ВидОрганизации]];
	Если ЭлементФормыПоУмолчанию <> Неопределено Тогда
		ЭлементФормыПоУмолчанию.АктивизироватьПоУмолчанию = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьФормуВРежимеПовторногоВыбораСистемыНалогообложения()

	Если ПараметрыУчетнойПолитики.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная") Тогда
		СистемаНалогообложения = ?(ПараметрыУчетнойПолитики.ОбъектНалогообложенияУСН = ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.Доходы"),
			"УпрощеннаяДоходы", "УпрощеннаяДоходыМинусРасходы");
	ИначеЕсли ПараметрыУчетнойПолитики.СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок") 
		    И ПараметрыУчетнойПолитики.ПрименяетсяУСНПатент Тогда
		СистемаНалогообложения = "Патентная";
	Иначе
		СистемаНалогообложения = "Общая";
	КонецЕсли;
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы[ТекущаяСтраницаПоВидуОрганизации(ВидОрганизации, Истина)];
	Элементы.НазадЮЛ.Видимость = Ложь;
	Элементы.НазадИП.Видимость = Ложь;
	Элементы.ОтступЮЛ.Видимость = Истина;
	Элементы.ОтступИП.Видимость = Истина;
	
	КнопкаПоУмолчанию = Новый Соответствие;
	КнопкаПоУмолчанию["ЮридическоеЛицо"]                                               = Новый Соответствие;
	КнопкаПоУмолчанию["ЮридическоеЛицо"]["УпрощеннаяДоходы"]                           = "СистемаНалогообложенияУСНДоходыЮЛ";
	КнопкаПоУмолчанию["ЮридическоеЛицо"]["УпрощеннаяДоходыМинусРасходы"]               = "СистемаНалогообложенияУСНДоходыМинусРасходыЮЛ";
	КнопкаПоУмолчанию["ЮридическоеЛицо"]["Общая"]                                      = "СистемаНалогообложенияОбщаяЮЛ";
	КнопкаПоУмолчанию["ИндивидуальныйПредприниматель"]                                 = Новый Соответствие;
	КнопкаПоУмолчанию["ИндивидуальныйПредприниматель"]["УпрощеннаяДоходы"]             = "СистемаНалогообложенияУСНДоходыИП";
	КнопкаПоУмолчанию["ИндивидуальныйПредприниматель"]["УпрощеннаяДоходыМинусРасходы"] = "СистемаНалогообложенияУСНДоходыМинусРасходыИП";
	КнопкаПоУмолчанию["ИндивидуальныйПредприниматель"]["Патентная"]                    = "СистемаНалогообложенияПатентнаяИП";
	КнопкаПоУмолчанию["ИндивидуальныйПредприниматель"]["Общая"]                        = "СистемаНалогообложенияОбщаяИП";
	
	ЭлементФормыПоУмолчанию = Элементы[КнопкаПоУмолчанию[ВидОрганизации][СистемаНалогообложения]];
	Если ЭлементФормыПоУмолчанию <> Неопределено Тогда
		ЭлементФормыПоУмолчанию.АктивизироватьПоУмолчанию = Истина;
	КонецЕсли;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтруктуруУчетнойПолитики(ВидОрганизации, СистемаНалогообложения, ПлательщикЕНВД)
	
	Возврат РегистрыСведений.УчетнаяПолитикаОрганизаций.ПолучитьСтруктуруУчетнойПолитики(ВидОрганизации, СистемаНалогообложения, ПлательщикЕНВД);
	
КонецФункции

#КонецОбласти
